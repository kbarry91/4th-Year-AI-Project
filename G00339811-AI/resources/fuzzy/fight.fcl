FUNCTION_BLOCK fight	// Block definition (there may be more than one block per file)
	
	// Define input variables
	VAR_INPUT				
		weapon : REAL;
		opponent : REAL;
	END_VAR
	
	// Define output variable
	VAR_OUTPUT				
		damage : REAL;
	END_VAR
	
	FUZZIFY weapon
	 TERM harmless := (0, 1) (20, 1) (60, 0);
	 TERM dangerous := trian 20 50 80;
	 TERM lethal := (40, 0) (80, 1) (100, 1);
	END_FUZZIFY
	
	FUZZIFY opponent
	 TERM weak := trian 0 20 40;
	 TERM strong := trian 30 50 70;
	 TERM formidable := trian 60 80 100;
	END_FUZZIFY
	
	
	DEFUZZIFY damage
	 TERM small := (19,0) (20, 1) (21, 0);
	 TERM partial := (49,0) (50, 1) (51, 0);
	 TERM great := (79,0) (80, 1) (81, 0);
	 METHOD : COG;
	 DEFAULT := 0;
	END_DEFUZZIFY
	
	RULEBLOCK BlockA
	 AND : MIN;
	 ACT : MIN;
	 ACCU : MAX;
	 RULE 1 : IF weapon IS dangerous AND opponent IS strong THEN damage IS partial;
	 RULE 2 : IF weapon IS lethal AND opponent IS formidable THEN damage IS great;
	 RULE 3 : IF weapon IS harmless OR opponent IS weak THEN damage IS small;
	END_RULEBLOCK

END_FUNCTION_BLOCK